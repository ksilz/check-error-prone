plugins {
    id 'org.springframework.boot' version '2.1.12.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'

    // additions
    id 'net.ltgt.errorprone' version "1.1.1"
}

group = 'com.betterprojectsfaster.test.errorprone'
version = '1.0'

sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // additions
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    errorprone 'com.google.errorprone:error_prone_core:2.3.4'

}

// additions from here on out
targetCompatibility = '11'
assert System.properties["java.specification.version"] == "11"

defaultTasks "bootRun"

springBoot {
    mainClassName = "com.betterprojectsfaster.test.errorprone.lombokErrorProne.LombokErrorProneApplication"
}

// addition
tasks.withType(JavaCompile) {
    options.errorprone {
        disableWarningsInGeneratedCode = true
        allDisabledChecksAsWarnings = true

        /*
            COMMENT OUT THIS LINE AND UN-COMMENT ONE OF THE "excludedPaths"
            LINES BELOW TO REPRODUCE ERRORS
        */
        excludedPaths = ".*/issues/.*"

        /*
            DOESN'T REPRODUCE THE ERROR - IGNORE FOR NOW!

            https://github.com/google/error-prone/issues/1478
                UnnecessaryDefaultInEnumSwitch: NPE in SuggestedFixes.compilesWithFix
                (SuggestedFixes.java:1019)
        */
        //  excludedPaths = ".*/ issues / almostjavadoc /.* "

        /*
            https://github.com/google/error-prone/issues/1479
                MultiVariableDeclaration: IllegalArgumentException
                "Start [449] should not be after end [-1]" (Preconditions.java:300) in Lombok class
        */
        // excludedPaths = ".*/ issues / (almostjavadoc | unnecessarydefaultinenumswitch) /.* "

    }
    options.errorprone.disable("AndroidJdkLibsChecker", "Java7ApiChecker")
}


